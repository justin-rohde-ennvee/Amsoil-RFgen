<Record FileDesc="Amsoil Shipping" FileVersion="5.0.8.0" Desc="Amsoil Shipping" />
<Code>
'#Language "WWB-COM"
Option Explicit

Public Type ShipmentRequest
  sCarrier As String
  nWeight As Currency
  sAccount As String
  fromAddress As AddressInfo
  toAddress As AddressInfo
End Type

Public Type ShipmentResponse
  sTrackingNumber As String
  nCost As Currency
  sLabelImageBytes As String
  sErrMsg As String
End Type

Public Function PrintCartonIDLabel( _
  ByVal sLPID As String, _
  ByVal sEQTY As String, _
  ByVal nDOCO As String, _
  ByVal nSequence As Long, _
  ByVal nCount As Long, _
  ByVal sWave As String, _
  ByVal nPrinterNumber As Long, _
  sErrMsg As String _
) As Boolean
  Dim sDateTime As String
  Dim sSrc As String
  Dim sDst As String
  Dim sJobName As String
  Dim sUser As String

  On Error GoTo OnError

  'Create job name and output file
  sDateTime = Trim(Format(Time, "hhnnss"))
  sUser = UCase(Trim(App.User))
  sJobName = "CARTON_" & sLPID & "_" & sDateTime & "_" & sUser
  sSrc = SYS.EnvironmentProperty("LPSStagingFolder") & sJobName & ".pas"
  sDst = SYS.EnvironmentProperty("LPSDropFolder") & sJobName & ".pas"

  Open sSrc For Output As #1

  Print #1, "*Format," & "CARTON.lwl"
  Print #1, "*JOBNAME," & sJobName
  Print #1, "*QUANTITY," & 1
  Print #1, "*PRINTERNUMBER," & nPrinterNumber
  Print #1, "CARTON_ID," & sLPID
  Print #1, "ORDER_NUMBER," & nDOCO
  Print #1, "CARTON_NAME," & sEQTY
  Print #1, "NUMBER_COUNT," & nSequence & "/" & nCount
  Print #1, "USER," & sUser
  Print #1, "WAVE," & FixRight(sWave, 12)
  Print #1, "*PRINTLABEL"

  Close #1

  FileCopy(sSrc, sDst)
  Kill(sSrc)

OnError:
  If Err.Number <> 0 Then
    sErrMsg = Err.Description
    Exit Function
  End If

  PrintCartonIDLabel = True
End Function

Private Type CartonContentsLineItem
  sLITM As String
  sDSC1 As String
  nQty As Currency
  sUOM As String
End Type

Private Type CartonContents
  nDOCO As Long
  nRecordCount As Long
  bContainsLQItem As Boolean
  tLineItems() As CartonContentsLineItem
End Type

Private Function FetchContents(ByVal sLPID) As CartonContents
  Dim sSQL As String
  Dim sCols As String
  Dim sRows As String
  Dim i As Long
  Dim tContents As CartonContents
  Dim tContentItem As CartonContentsLineItem

  sSQL = _
    " select max(LDRDOCO) LDRDOCO, LDLITM, sum(LDUORG) LDUORG," & _
    "        max(LDUOM) LDUOM, max(IMDSC1) IMDSC1, max(IIUNNA) IIUNNA" & _
    " from F55102" & _
    " inner join F4101 on IMLITM = LDLITM" & _
    " left join F4908 on IIITM = IMITM" & _
    " where LDLPID = '" & sLPID & "'" & _
    " group by LDLITM, LDUOM" & _
    " order by LDLITM, LDUOM"
  DB.Execute(sSQL, sCols, sRows)

  tContents.nRecordCount = DB.Count(sRows)
  tContents.nDOCO = DB.Extract(sCols, sRows, 1, "LDRDOCO")

  ReDim tContents.tLineItems(1 To tContents.nRecordCount)

  For i=1 To tContents.nRecordCount
    tContentItem.sLITM = Trim(DB.Extract(sCols, sRows, i, "LDLITM"))
    tContentItem.sDSC1 = Trim(DB.Extract(sCols, sRows, i, "IMDSC1"))
    tContentItem.nQty = Val(DB.Extract(sCols, sRows, i, "LDUORG"))
    tContentItem.sUOM = Trim(DB.Extract(sCols, sRows, i, "LDUOM"))

    If Trim(DB.Extract(sCols, sRows, i, "IIUNNA")) = "LQ" Then
      tContents.bContainsLQItem = True
    End If

    tContents.tLineItems(i) = tContentItem
  Next

  FetchContents = tContents
End Function

Public Function PrintCartonContentsLabel( _
  ByVal sLPID As String, _
  ByVal nPrinterNumber As Long, _
  sErrMsg As String _
) As Boolean
  On Error GoTo OnError

  Const cMaxLinesPerLabel = 12

  Dim sSrc As String
  Dim sDst As String
  Dim sJobName As String
  Dim i As Long
  Dim nLabelCount As Long
  Dim nLabelNumber As Long
  Dim nLabelLineNumber As Long
  Dim tContents As CartonContents
  Dim tLineItem As CartonContentsLineItem
  Dim nBoxNumber As Long
  Dim nBoxCount As Long
  Dim sFormat As String

  ' Temporary: print name & address
  Dim sSQL As String
  Dim sCols As String
  Dim sRows As String
  Dim nSHAN As Long
  Dim toAddress As AddressInfo

  sSQL = "select SDSHAN from F55102 inner join F4211 on SDDOCO = LDRDOCO where LDLPID = '" & sLPID & "'"
  DB.Execute(sSQL, sCols, sRows)
  nSHAN = Val(DB.Extract(sCols, sRows, 1, "SDSHAN"))
  toAddress = GetAddressInfo(nSHAN)

  ' End temp

  sJobName = "CONTENTS_" & sLPID & "_" & Format(Time, "hhnnss") & "_" & UCase(Trim(App.User))
  sSrc = SYS.EnvironmentProperty("LPSStagingFolder") & sJobName & ".pas"
  sDst = SYS.EnvironmentProperty("LPSDropFolder") & sJobName & ".pas"

  tContents = FetchContents(sLPID)
  nLabelCount = ((tContents.nRecordCount-1) / cMaxLinesPerLabel) + 1
  GetCartonNumberAndCount(sLPID, nBoxNumber, nBoxCount)

  If tContents.bContainsLQItem Then
    sFormat = "CONTENTS_LQ.lwl"
  Else
    sFormat = "CONTENTS.lwl"
  End If

  Open sSrc For Output As #1

  Print #1, "*Format," & sFormat
  Print #1, "*JOBNAME," & sJobName
  Print #1, "*QUANTITY," & 1
  Print #1, "*PRINTERNUMBER," & nPrinterNumber
  Print #1, "ORDER_NUMBER," & tContents.nDOCO
  Print #1, "CARTON_ID," & sLPID
  Print #1, "BOX_NUMBER_COUNT, Box " & nBoxNumber & "/" & nBoxCount

  ' Temp: add name & address to label
  Print #1, "NAME," & toAddress.sName
  Print #1, "ADDRESS," & Trim(toAddress.sLine1) & " " & _
                         Trim(toAddress.sLine2) & " " & _
                         Trim(toAddress.sLine3) & " " & _
                         Trim(toAddress.sCity) & " " & _
                         Trim(toAddress.sState) & " " & _
                         Trim(toAddress.sZip)
  ' End temp

  If tContents.nRecordCount = 0 Then
    Print #1, "ITEM_1" & ", No items."
    GoTo Flush
  End If

  nLabelNumber = 1
  For i=1 To tContents.nRecordCount
    nLabelLineNumber += 1
    tLineItem = tContents.tLineItems(i)

    Print #1, "ITEM_" & nLabelLineNumber & "," & tLineItem.sLITM
    Print #1, "DESC_" & nLabelLineNumber & "," & tLineItem.sDSC1
    Print #1, "QTY_" & nLabelLineNumber & "," & tLineItem.nQty
    Print #1, "UOM_" & nLabelLineNumber & "," & tLineItem.sUOM

    If nLabelLineNumber = cMaxLinesPerLabel Then
      Print #1, "PAGE_NUMBER_COUNT, Page " & nLabelNumber & "/" & nLabelCount
      Print #1, "*PRINTLABEL"
      nLabelLineNumber = 0
      nLabelNumber += 1
    End If
  Next

  If nLabelLineNumber > 1 Then
    ' Print final partial label
    Print #1, "NUMBER_COUNT," & nLabelNumber & "/" & nLabelCount
  End If

Flush:
  Print #1, "*PRINTLABEL"
  Close #1

  FileCopy(sSrc, sDst)
  Kill(sSrc)

  PrintCartonContentsLabel = True

OnError:
  If Err.Number <> 0 Then
    sErrMsg = "Error printing carton contents label: " & Err.Description
    Exit Function
  End If
End Function

Public Sub GetCartonNumberAndCount(ByVal sLPID As String, nNumber As Long, nCount As Long)
  Dim nDOCO As Long
  Dim sSQL As String
  Dim sCols As String
  Dim sRows As String
  Dim i As Long

  nDOCO = FetchFirstResult("select distinct LDRDOCO from F55102 where LDLPID = '" & sLPID & "'")

  sSQL = "select distinct LDLPID from F55102 where LDRDOCO = " & nDOCO & " order by LDLPID"
  DB.Execute(sSQL, sCols, sRows)

  For i=1 To DB.Count(sRows)
    If Trim(DB.Extract(sCols, sRows, i, "LDLPID")) = sLPID Then
      nNumber = i
      Exit For
    End If
  Next

  nCount = DB.Count(sRows)
End Sub

Public Function ItemWeight(ByVal sMCU As String, ByVal sLITM As String, ByVal sUOM As String) As Currency
  Dim sSQL As String
  Dim sCols As String
  Dim sRows As String
  Dim sITM As String
  Dim nQty As Currency

  ' Fetch short item
  sSQL = "select IMITM from F4101 where IMLITM = '" & sLITM & "'"
  DB.Execute(sSQL, sCols, sRows)
  sITM = DB.Extract(sCols, sRows, 1, "IMITM")

  ' Determine if this is a literature item
  sSQL = _
    " select count(*)" & _
    " from F4101" & _
    " inner join F41002 um " & _
    "   on um.UMITM = IMITM" & _
    "   and um.UMUM = IMUOM1" & _
    " inner join F41002 umweight " & _
    "   on umweight.UMITM = um.UMITM" & _
    "   and umweight.UMUM = CASE WHEN um.UMRUM = 'LB' THEN um.UMUM ELSE um.UMRUM END" & _
    "   and umweight.UMRUM = 'LB' " & _
    " where IMITM = " & sITM & _
    " and IMSRP4 in ('G07', 'G32', 'OAI')" & _
    " and ROUND((umweight.UMCONV * .0000001) * (um.UMCNV1 * .0000001), 4) <= .0001"
  DB.Execute(sSQL, sCols, sRows)

  If Val(sRows) > 0 Then
    ' Take weight from WH table, if exists
    sSQL = _
      " select IQGWEI" & _
      " from F46011" & _
      " where IQMCU = '" & sMCU & "'" & _
      " and IQITM = " & sITM & _
      " and IQUOM = 'EA'"
    DB.Execute(sSQL, sCols, sRows)

    If DB.Count(sRows) > 0 Then
      ItemWeight = ConvDecimalsFromSQL("GWEI", DB.Extract(sCols, sRows, 1, "IQGWEI"))
      Exit Function
    Else
      ' TODO Error/Warning?
    End If
  End If

  ' Take weight from standard conversion tables
  nQty = 1

  If sUOM <> "EA" Then
    ' First, convert to EA
    If Not ConvUOM(sITM, nQty, "EA", sUOM) Then Exit Function
  End If

  ' Convert to pounds
  If Not ConvUOM(sITM, nQty, "LB", "EA") Then Exit Function
  ItemWeight = nQty
End Function

Public Function PackageWeight(ByVal sLPID As String) As Currency
  Dim sSQL As String
  Dim sCols As String
  Dim sRows As String
  Dim i As Long
  Dim sLITM As String
  Dim nQty As Currency
  Dim sUOM As String
  Dim sMCU As String
  Dim nItemWeight As Currency

  sSQL = _
    " select LMMCU, LDLITM, LDUORG, LDUOM" & _
    " from F55102" & _
    " inner join F55101 on LMLPID = LDLPID" & _
    " where LDLPID = '" & sLPID & "'"
  DB.Execute(sSQL, sCols, sRows)

  For i=1 To DB.Count(sRows)
    sLITM = Trim(DB.Extract(sCols, sRows, i, "LDLITM"))
    nQty = Val(DB.Extract(sCols, sRows, i, "LDUORG"))
    sUOM = Trim(DB.Extract(sCols, sRows, i, "LDUOM"))
    sMCU = DB.Extract(sCols, sRows, i, "LMMCU")

    nItemWeight = ItemWeight(sMCU, sLITM, sUOM) * nQty
    PackageWeight += nItemWeight
  Next
End Function

Public Function PickSlipNumberForCarton(ByVal sLPID As String)
  Dim sSQL As String

  sSQL = _
    " select distinct SDPSN" & _
    " from F55101" & _
    " inner join F55102" & _
    "   on LDLPID = LMLPID" & _
    " left join F4211" & _
    "   on SDMCU = LMMCU" & _
    "   and SDDOCO = LDRDOCO" & _
    "   and SDLNID = LDRLLN" & _
    " where LMLPID = '" & sLPID & "'"

  PickSlipNumberForCarton = FetchFirstResult(sSQL)

  If PickSlipNumberForCarton = "" Then
    sSQL = _
      " select distinct SDPSN" & _
      " from F55101" & _
      " inner join F55102" & _
      "   on LDLPID = LMLPID" & _
      " left join F42119" & _
      "   on SDMCU = LMMCU" & _
      "   and SDDOCO = LDRDOCO" & _
      "   and SDLNID = LDRLLN" & _
      " where LMLPID = '" & sLPID & "'"

    PickSlipNumberForCarton = FetchFirstResult(sSQL)
  End If
End Function

Public Function GetCartonSequence(ByVal sLPID As String, ByVal sPSN As String, nSequenceNumber As Long, nCartonCount As Long, sErrMsg) As Boolean
  Dim sSQL As String
  Dim sCols As String
  Dim sRows As String
  Dim i As Long

  ' Get carton count from cartonization
  sSQL = "select distinct R2RQSQ from F4611 where R2RQBT = " & sPSN
  DB.Execute(sSQL, sCols, sRows)

  nCartonCount = DB.Count(sRows)

  If nCartonCount = 0 Then
    sErrMsg = "Unable to determine carton count for pick slip #" & sPSN
    Return False
  End If

  ' Get sequence # of this individual box
  sSQL = _
    " select LMLPID" & _
    " from F55101" & _
    " inner join F55102 on LDLPID = LMLPID" & _
    " left join F4211 on SDMCU = LMMCU and SDDOCO = LDRDOCO and SDLNID = LDRLLN" & _
    " where SDPSN = " & sPSN & _
    " group by LMLPID" & _
    " order by LMLPID"
  DB.Execute(sSQL, sCols, sRows)

  If DB.Count(sRows) = 0 Then
    ' Use history instead
    sSQL = _
      " select LMLPID" & _
      " from F55101" & _
      " inner join F55102 on LDLPID = LMLPID" & _
      " left join F42119 on SDMCU = LMMCU and SDDOCO = LDRDOCO and SDLNID = LDRLLN" & _
      " where SDPSN = " & sPSN & _
      " group by LMLPID" & _
      " order by LMLPID"
    DB.Execute(sSQL, sCols, sRows)
  End If

  For i=1 To DB.Count(sRows)
    If sLPID = Trim(DB.Extract(sCols, sRows, i, "LMLPID")) Then
      nSequenceNumber = i
      Return True
    End If
  Next

  sErrMsg = "Unable to determine sequence # for carton ID: " & sLPID
  Return False
End Function

Public Function HasCarrierForPSN(ByVal sPSN As String, ByVal sCarrierName As String) As Boolean
  Dim sSQL As String
  Dim sCols As String
  Dim sRows As String

  sSQL = _
    " select count(*)" & _
    " from F554602" & _
    " where CFPSN = " & sPSN & _
    " and CFDL02 = '" & sCarrierName & "'" & _
    " and CFCNID = ' '"
  DB.Execute(sSQL, sCols, sRows)

  HasCarrierForPSN = Val(sRows) > 0
End Function

Public Function GetSelectedCarrierForPSN(ByVal sPSN As String) As String
  Dim sSQL As String
  Dim sCols As String
  Dim sRows As String

  sSQL = _
    " select CFDL02" & _
    " from F554602" & _
    " where CFPSN = " & sPSN & _
    " and CFCNID <> ' '" & _
    " order by case CFDL02" & _
    "   when 'UPS' then 1" & _
    "   when 'FedEx' then 2" & _
    "   else 3" & _
    " end"
  DB.Execute(sSQL, sCols, sRows)

  GetSelectedCarrierForPSN = Trim(DB.Extract(sCols, sRows, 1, "CFDL02"))
End Function

Public Function SearchCarriersByPrice(ByVal Rsp As String, ByVal sPSN As String) As Boolean
  Dim sSQL As String
  Dim sCols As String
  Dim sRows As String
  Dim oList As New SearchList
  Dim sSelection As String

  sSQL = _
    " select CFDL02, CFECST" & _
    " from F554602" & _
    " where CFPSN = " & sPSN & _
    " and CFCNID = ' '" & _
    " and CFECST > 0" & _
    " order by CFECST"

  oList.SetColumn(1, "Name", -1, TextLeft, True)
  oList.SetColumn(2, "Price", -1, TextRight, True)
  oList.SQL = sSQL
  Rsp = oList.ShowList

  SearchCarriersByPrice = Rsp <> ""
End Function

Public Function ValidateCarton(ByVal sMCU As String, ByVal sLPID As String) As Boolean
  Dim sSQL As String

  sSQL = _
    " select count(*)" & _
    " from F55101" & _
    " where LMLPID = '" & sLPID & "'" & _
    " and LMMCU = '" & sMCU & "'"
  ValidateCarton = FetchFirstResult(sSQL) > 0
End Function

Public Function GetTrackingNumber(ByVal sLPID As String) As String
  Dim sSQL As String
  sSQL = "select CFDL01 from F554602 where CFCNID = '" & sLPID & "'"
  GetTrackingNumber = Trim(FetchFirstResult(sSQL))
End Function

Public Function UpdatePackageInformation( _
  ByVal sLPID As String, _
  ByVal sTrackingNumber As String, _
  ByVal nCost As Currency, _
  sErrMsg As String _
) As Boolean
  Dim sSQL As String
  Dim nErrNo As Long

  sSQL = _
    " update F55101" & _
    " set LMVR01 = '" & sTrackingNumber & "'" & _
    " ,   LMVR02 = '" & nCost & "'" & _
    " where LMLPID = '%tmLPID'"
  nErrNo = DB.Execute(sSQL)

  If nErrNo <> 0 Then
    sErrMsg = "Error updating tracking and cost: " & nErrNo
    Exit Function
  End If

  UpdatePackageInformation = True
End Function

Private Function WriteImage(ByVal sBytes As String, ByVal sFilename As String, sErrMsg As String) As Boolean
  On Error GoTo OnError

  Dim xml As New MSXML2.DOMDocument60
  Dim node As IXMLDOMElement
  Dim outStream As New ADODB.Stream

  WriteImage = False

  Set node = xml.createElement("base64")
  node.dataType = "bin.base64"
  node.text = sBytes

  outStream.Type = adTypeBinary
  outStream.open
  outStream.Write(node.nodeTypedValue)
  outStream.SaveToFile(sFilename, adSaveCreateOverWrite)
  outStream.Close

  WriteImage = True

OnError:
  ' clean up
  If Not outStream Is Nothing Then
    If outStream.State = adStateOpen Then outStream.Close
  End If
  Set outStream = Nothing

  If Err.Number <> 0 Then sErrMsg = "Unable to save label image: (" & Err.Number & ") " & Err.Description
End Function

Public Function SaveShippingLabel( _
  ByVal sCarrier As String, _
  ByVal sLPID As String, _
  ByVal sExtension As String, _
  ByVal sImageBytes As String, _
  sErrMsg As String _
) As Boolean
  On Error GoTo OnError

  Dim sImageFile As String

  sImageFile = SYS.EnvironmentProperty("LPSImagesFolder") & "Shipping\" & sLPID & "." & sExtension
  If Not WriteImage(sImageBytes, sImageFile, sErrMsg) Then Exit Function

  Return True
OnError:
  If Err.Number <> 0 Then sErrMsg = Err.Description
End Function

Public Function WriteZPL( _
  ByVal sLPID As String, _
  ByVal nPrinterNumber As Long, _
  ByVal sZPL As String, _
  sErrMsg As String _
) As Boolean
  On Error GoTo OnError

  Dim sJobName As String
  Dim sFilename As String
  Dim sSrc As String
  Dim sDst As String

  sJobName = sLPID & "_" & Format(Time, "hhnnss") & "_" & UCase(Trim(App.User))
  sSrc = SYS.EnvironmentProperty("LPSStagingFolder") & sJobName & ".pas"
  sDst = SYS.EnvironmentProperty("LPSDropFolder") & sJobName & ".pas"

  Open sSrc For Output As #1
  Print #1, "*Format,Fedex.lwl"
  Print #1, "*JOBNAME," & sJobName
  Print #1, "*QUANTITY,1"
  Print #1, "*PRINTERNUMBER," & nPrinterNumber
  Print #1, "ptfield_zpl," & sZPL
  Print #1, "*PRINTLABEL"
  Close #1

  FileCopy(sSrc, sDst)
  Kill(sSrc)

  Return True

OnError:
  If Err.Number <> 0 Then sErrMsg = Err.Description
End Function

Public Function PrintZPL( _
  ByVal sLPID As String, _
  ByVal sZPL As String, _
  ByVal sPrinterName As String, _
  sErrMsg As String _
) As Boolean
  On Error GoTo OnError

  Dim objShell As Object
  Dim sSrc As String
  Dim sBat As String

  Set objShell = CreateObject("Shell.Application")
  sSrc = SYS.EnvironmentProperty("LPSImagesFolder") & "Shipping\" & sLPID & ".zpl"
  sBat = SYS.EnvironmentProperty("LPSImagesFolder") & "Shipping\" & sLPID & ".bat"

  Open sSrc For Output As #1
'  Print #1, "~JA"
  Print #1, sZPL
  Close #1

  Open sBat For Output As #1
  Print #1, "net use lpt1 /delete"
  Print #1, "net use lpt1 \\localhost\" & sPrinterName
  Print #1, "copy " & sSrc & " lpt1"
  Close #1

  objShell.ShellExecute(sBat)
  Set objShell = Nothing
  PrintZPL = True

OnError:
  If Err.Number <> 0 Then sErrMsg = "(" & Err.Number & ") " & Err.Description
End Function

Public Function ReprintZPL( _
  ByVal sLPID As String, _
  ByVal sPrinterName As String, _
  sErrMsg As String _
) As Boolean
  On Error GoTo OnError

  Dim objShell As Object
  Dim sSrc As String
  Dim sBat As String

  Set objShell = CreateObject("Shell.Application")
  sSrc = SYS.EnvironmentProperty("LPSImagesFolder") & "Shipping\" & sLPID & ".zpl"
  sBat = SYS.EnvironmentProperty("LPSImagesFolder") & "Shipping\" & sLPID & ".bat"

  Open sBat For Output As #1
  Print #1, "net use lpt1 /delete"
  Print #1, "net use lpt1 \\localhost\" & sPrinterName
  Print #1, "copy " & sSrc & " lpt1"
  Close #1

  objShell.ShellExecute(sBat)
  Set objShell = Nothing
  ReprintZPL = True

OnError:
  If Err.Number <> 0 Then sErrMsg = "(" & Err.Number & ") " & Err.Description
End Function


Public Function PrintShippingLabel( _
  ByVal sCarrier As String, _
  ByVal sLPID As String, _
  ByVal nPrinterNumber As Long, _
  sErrMsg As String _
) As Boolean
  Dim sJobName As String
  Dim sUser As String
  Dim sSrc As String
  Dim sDst As String

  On Error GoTo OnError

  sJobName = sCarrier & "_" & sLPID & "_" & Format(Time, "hhnnss") & "_" & UCase(Trim(App.User))
  sSrc = SYS.EnvironmentProperty("LPSStagingFolder") & sJobName & ".pas"
  sDst = SYS.EnvironmentProperty("LPSDropFolder") & sJobName & ".pas"

  Open sSrc For Output As #1

  Print #1, "*Format," & sCarrier & "_Dynamic.lwl"
  Print #1, "*JOBNAME," & sJobName
  Print #1, "*QUANTITY," & 1
  Print #1, "*PRINTERNUMBER," & nPrinterNumber
  Print #1, "IMAGE_NAME," & sLPID & ".gif"
  Print #1, "*PRINTLABEL"

  Close #1

  FileCopy(sSrc, sDst)
  Kill(sSrc)

  Return True

OnError:
  If Err.Number <> 0 Then sErrMsg = Err.Description
End Function
</Code>
