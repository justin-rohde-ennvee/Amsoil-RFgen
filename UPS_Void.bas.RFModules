<Record FileDesc="UPS Void API" FileVersion="5.0.8.0" Desc="UPS Void API" Group="UPS" />
<Code>
Option Explicit
'#Language "WWB-COM"

Public Const UPS_TEST_TRACKING_NUMBER_VOID_SUCCESS = "1Z2220060290602143"

Public Function UPS_VoidShipment(ByVal sTrackingNumber As String, sErrMsg As String) As Boolean
  Dim sToken As String
  Dim sResponseText As String
  Dim jsonParser As New VbsJson
  Dim oResponseJson As Scripting.Dictionary
  Dim sCode As String
  Dim sDescription As String

  If Not UPS_GetToken(sToken, sErrMsg) Then Exit Function
  If Not UPS_SendVoidRequest(sToken, sTrackingNumber, sResponseText, sErrMsg) Then Exit Function

  Set oResponseJson = jsonParser.Decode(sResponseText)
  If Not UPS_ParseVoidResponse(oResponseJson, sCode, sDescription, sErrMsg) Then Exit Function

  If sCode = "1" Then Return True

  sErrMsg = sDescription
  Return False
End Function

Private Function UPS_SendVoidRequest( _
  ByVal sToken As String, _
  ByVal sTrackingNumber As String, _
  sResponseText As String, _
  sErrMsg As String _
) As Boolean
  Dim sURL As String
  Dim oHttp As New XMLHTTP60

  sURL = UPS_BASE_URL & "/api/shipments/v1/void/cancel/" & sTrackingNumber

  oHttp.open("DELETE", sURL, False)
  oHttp.setRequestHeader("Authorization", "Bearer " & sToken)
  oHttp.Send()

  If oHttp.status <> 200 Then
    sErrMsg = "(" & oHttp.status & ") " & oHttp.statusText
    Return False
  End If

  sResponseText = oHttp.responseText
  Return True
End Function

Private Function UPS_ParseVoidResponse( _
  ByVal oResponse As Scripting.Dictionary, _
  sCode As String, _
  sDescription As String, _
  sErrMsg As String _
) As Boolean
  Dim oVoidShipmentResponse As New Scripting.Dictionary
  Dim oSummaryResult As New Scripting.Dictionary
  Dim oCharges As New Scripting.Dictionary

  If Not oResponse.Exists("VoidShipmentResponse") Then
    sErrMsg = "Missing response element: 'VoidShipmentResponse'"
    Return False
  End If

  Set oVoidShipmentResponse = oResponse("VoidShipmentResponse")
  If Not oVoidShipmentResponse.Exists("SummaryResult") Then
    sErrMsg = "Missing response element: 'SummaryResult'"
    Return False
  End If

  Set oSummaryResult = oVoidShipmentResponse("SummaryResult")
  sCode = oSummaryResult("Status")("Code")
  sDescription = oSummaryResult("Status")("Description")

  Return True
End Function
</Code>
