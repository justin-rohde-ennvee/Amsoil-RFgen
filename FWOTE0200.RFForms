<Record FileDesc="WO - Time Entry Start/Stop" FileVersion="5.0.8.0" Desc="WO - Time Entry Start/Stop" Group="WO" LinkTo="No Links" LinkType="0" LinkMode="0" PromptList="txtMCU&vm;txtWODT&vm;txtEMPL&vm;txtDOCO&vm;txtDCTO&vm;lblItem&vm;lblItemDesc&vm;txtSHIFT&vm;txtHRTYPE&vm;txtOPSQ&vm;txtSTIME&vm;txtETIME&vm;txtQty&vm;txtAccept&vm;lblMaxQty">
<Schema>
<SchemaParam Linked="0" Attr="1" Name="txtMCU" />
<SchemaParam Linked="0" Attr="2" Name="txtWODT" />
<SchemaParam Linked="0" Attr="3" Name="txtEMPL" />
<SchemaParam Linked="0" Attr="4" Name="txtDOCO" />
<SchemaParam Linked="0" Attr="5" Name="txtDCTO" />
<SchemaParam Linked="0" Attr="6" Name="lblItem" />
<SchemaParam Linked="0" Attr="7" Name="lblItemDesc" />
<SchemaParam Linked="0" Attr="8" Name="txtSHIFT" />
<SchemaParam Linked="0" Attr="9" Name="txtHRTYPE" />
<SchemaParam Linked="0" Attr="10" Name="txtOPSQ" />
<SchemaParam Linked="0" Attr="11" Name="txtSTIME" />
<SchemaParam Linked="0" Attr="12" Name="txtETIME" />
<SchemaParam Linked="0" Attr="13" Name="txtQty" />
<SchemaParam Linked="0" Attr="14" Name="txtAccept" />
<SchemaParam Linked="0" Attr="15" Name="lblMaxQty" />
</Schema>
<Displays>
<Display Name="EnglishGUI" Type="1" Width="1920" Height="7040" Locale="1033" />
</Displays>
<Form FieldId="Form" Attr="0" LinkMode="0" Pages="1">
<Controls>
<Control Type="1" FieldId="txtMCU" Attr="1" Defaults="@LAST" KeyField="0" MaskInput="0" Required="1">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="0" Top="22" Width="55" Height="22" AnchorRight="185" AnchorBottom="276" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="Plant:" />
<Field Align="0" Anchor="3" AutoSize="0" BorderStyle="3" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="48" Top="22" Width="111" Height="22" AnchorRight="81" AnchorBottom="276" BackColor1="000001" BackColor2="000001" ForeColor="000001" DisplayOnly="0" />
</Layout>
</Layouts>
</Control>
<Control Type="1" FieldId="txtWODT" Attr="2" Defaults="@DATE" KeyField="0" MaskInput="0" Required="0">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="0" Top="44" Width="55" Height="22" AnchorRight="185" AnchorBottom="254" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="Date :" />
<Field Align="0" Anchor="3" AutoSize="0" BorderStyle="3" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="48" Top="44" Width="111" Height="22" AnchorRight="81" AnchorBottom="254" BackColor1="000001" BackColor2="000001" ForeColor="000001" DisplayOnly="0" />
</Layout>
</Layouts>
</Control>
<Control Type="1" FieldId="txtEMPL" Attr="3" Defaults="@LAST" KeyField="0" MaskInput="0" Required="1">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="0" Top="66" Width="55" Height="22" AnchorRight="185" AnchorBottom="232" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="Empl#:" />
<Field Align="0" Anchor="3" AutoSize="0" BorderStyle="3" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="48" Top="66" Width="111" Height="22" AnchorRight="81" AnchorBottom="232" BackColor1="000001" BackColor2="000001" ForeColor="000001" DisplayOnly="0" />
</Layout>
</Layouts>
</Control>
<Control Type="1" FieldId="txtDOCO" Attr="4" Defaults="@LAST" KeyField="0" MaskInput="0" Required="1">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="16" Top="88" Width="39" Height="22" AnchorRight="185" AnchorBottom="210" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="WO#:" />
<Field Align="0" Anchor="3" AutoSize="0" BorderStyle="3" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="48" Top="88" Width="111" Height="22" AnchorRight="81" AnchorBottom="210" BackColor1="000001" BackColor2="000001" ForeColor="000001" DisplayOnly="0" />
</Layout>
</Layouts>
</Control>
<Control Type="1" FieldId="txtDCTO" Attr="5" Defaults="@LAST" KeyField="0" MaskInput="0" Required="1">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="160" Top="88" Width="47" Height="22" AnchorRight="33" AnchorBottom="210" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="Type:" />
<Field Align="0" Anchor="3" AutoSize="0" BorderStyle="3" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="200" Top="88" Width="39" Height="22" AnchorRight="1" AnchorBottom="210" BackColor1="000001" BackColor2="000001" ForeColor="000001" DisplayOnly="0" />
</Layout>
</Layouts>
</Control>
<Control Type="10" FieldId="lblItem" Attr="6">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="0" Top="110" Width="63" Height="22" AnchorRight="177" AnchorBottom="188" BackColor1="000001" BackColor2="000001" ForeColor="00FFFF" Caption="lblItem" />
</Layout>
</Layouts>
</Control>
<Control Type="10" FieldId="lblItemDesc" Attr="7">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="0" Top="132" Width="95" Height="22" AnchorRight="145" AnchorBottom="166" BackColor1="000001" BackColor2="000001" ForeColor="00FFFF" Caption="lblItemDesc" />
</Layout>
</Layouts>
</Control>
<Control Type="1" FieldId="txtSHIFT" Attr="8" Defaults="@LAST" KeyField="0" MaskInput="0" Required="1">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="0" Top="154" Width="55" Height="22" AnchorRight="185" AnchorBottom="144" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="Shift:" />
<Field Align="0" Anchor="3" AutoSize="0" BorderStyle="3" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="48" Top="154" Width="55" Height="22" AnchorRight="137" AnchorBottom="144" BackColor1="000001" BackColor2="000001" ForeColor="000001" DisplayOnly="0" />
</Layout>
</Layouts>
</Control>
<Control Type="1" FieldId="txtHRTYPE" Attr="9" Defaults="@LAST" KeyField="0" MaskInput="0" Required="1">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="128" Top="154" Width="55" Height="22" AnchorRight="57" AnchorBottom="144" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="Type :" />
<Field Align="0" Anchor="3" AutoSize="0" BorderStyle="3" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="184" Top="154" Width="55" Height="22" AnchorRight="1" AnchorBottom="144" BackColor1="000001" BackColor2="000001" ForeColor="000001" DisplayOnly="0" />
</Layout>
</Layouts>
</Control>
<Control Type="1" FieldId="txtOPSQ" Attr="10" KeyField="0" MaskInput="0" Required="1">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="0" Top="176" Width="55" Height="22" AnchorRight="185" AnchorBottom="122" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="Oper#:" />
<Field Align="0" Anchor="3" AutoSize="0" BorderStyle="3" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="48" Top="176" Width="79" Height="22" AnchorRight="113" AnchorBottom="122" BackColor1="000001" BackColor2="000001" ForeColor="000001" DisplayOnly="0" />
</Layout>
</Layouts>
</Control>
<Control Type="1" FieldId="txtSTIME" Attr="11" KeyField="0" MaskInput="0" Required="1">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="0" Top="198" Width="55" Height="22" AnchorRight="185" AnchorBottom="100" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="Start:" />
<Field Align="0" Anchor="3" AutoSize="0" BorderStyle="3" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="48" Top="198" Width="71" Height="22" AnchorRight="121" AnchorBottom="100" BackColor1="000001" BackColor2="000001" ForeColor="000001" DisplayOnly="0" />
</Layout>
</Layouts>
</Control>
<Control Type="1" FieldId="txtETIME" Attr="12" KeyField="0" MaskInput="0" Required="1">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="8" Top="220" Width="39" Height="22" AnchorRight="193" AnchorBottom="78" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="End:" />
<Field Align="0" Anchor="3" AutoSize="0" BorderStyle="3" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="48" Top="220" Width="79" Height="22" AnchorRight="113" AnchorBottom="78" BackColor1="000001" BackColor2="000001" ForeColor="000001" DisplayOnly="0" />
</Layout>
</Layouts>
</Control>
<Control Type="1" FieldId="txtQty" Attr="13" KeyField="0" MaskInput="0" Required="1">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="8" Top="242" Width="39" Height="22" AnchorRight="193" AnchorBottom="56" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="Qty:" />
<Field Align="0" Anchor="3" AutoSize="0" BorderStyle="3" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="48" Top="242" Width="87" Height="22" AnchorRight="105" AnchorBottom="56" BackColor1="000001" BackColor2="000001" ForeColor="000001" DisplayOnly="0" />
</Layout>
</Layouts>
</Control>
<Control Type="1" FieldId="txtAccept" Attr="14" KeyField="0" MaskInput="0" Required="0">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="0" Top="264" Width="151" Height="22" AnchorRight="89" AnchorBottom="34" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="Enter to accept..." />
<Field Align="0" Anchor="3" AutoSize="0" BorderStyle="3" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="144" Top="264" Width="0" Height="22" AnchorRight="96" AnchorBottom="34" BackColor1="000001" BackColor2="000001" ForeColor="000001" DisplayOnly="0" />
</Layout>
</Layouts>
</Control>
<Control Type="10" FieldId="lblMaxQty" Attr="15">
<Layouts>
<Layout PageNo="1" Visible="1">
<Label Anchor="3" AutoSize="1" BorderStyle="5" BackFill="0" FontSize="0" FontStyle="0" MultiLine="0" Left="136" Top="242" Width="79" Height="22" AnchorRight="25" AnchorBottom="56" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="lblMaxQty" />
</Layout>
</Layouts>
</Control>
</Controls>
<Layouts>
<Layout Visible="1" FormWidth="240" FormHeight="320" Scrollbars="0">
<Label Align="1" AutoSize="2" FontSize="0" FontStyle="0" Width="0" Height="22" BackColor1="000001" BackColor2="000001" ForeColor="000001" Caption="[WO - Time Entry]" />
<Field AutoSize="0" BorderStyle="3" DropShadow="1" FontSize="0" FontStyle="0" BackColor1="000001" BackColor2="000001" ForeColor="000001" />
</Layout>
</Layouts>
</Form>
</Record>
<Code>
' -------------------------------------------------------------------------------
' | (C)opyright 2003-2012 The DataMAX Software Group, Inc., All Rights Reserved.|
' | RFgen JD Edwards Integration Suite Vers.# 420                               |
' -------------------------------------------------------------------------------
'
' WORK ORDERS: TIME ENTRY START/STOP
'
' NOTES:
'
' MODIFICATION HISTORY:
'

Option Explicit

Private Const cTNId     = "FWOTE0200"
Private Const cUse24Hrs = True
Private Const cStatusF  = 40
Private msPgm           As String
Private msZJDEMAIN      As String
Private msDOCTYPE       As String
Private mnStatusT       As Currency
Private mnMaxQty        As Currency
Private bNewWO          As Boolean

Private Sub Form_GotFocus(ByRef Rsp As String, ByRef AllowChange As Boolean)
  On Error Resume Next
  '
  Dim iCN As Integer
  '
  iCN = App.PromptNo
  RFPrompt(iCN).BorderStyle = DisplayStandard
  RFPrompt(iCN).BackColor1 = cFieldFocusBC
  RFPrompt(iCN).ForeColor = cFieldFocusFC

End Sub

Private Sub Form_Load()
  On Error Resume Next
  '
  Call SetDisplay()
  '
  Dim sHeader As String

  ' Initialize the RFGen recordset with extra fields
  App.SetValue("tmMCU",    "")
  App.SetValue("tmCO",     "")
  App.SetValue("tmCSMT",   "")
  App.SetValue("tmEMPL",   "")
  App.SetValue("tmSHIFT",  "")
  App.SetValue("tmDOCO",   "")
  App.SetValue("tmDCTO",   "")
  App.SetValue("tmOPSQ",   "")
  App.SetValue("tmHRTYPE", "")
  App.SetValue("tmSTIME",  "")
  App.SetValue("tmETIME",  "")
  '
  ' Initialize special controls
  lblItem.Caption     = ""
  lblItem.Label.BackColor1 = cFieldDefaultBC
  lblItem.Label.ForeColor = cFieldDefaultFC
  '
  lblItemDesc.Caption = ""
  lblItemDesc.Label.BackColor1 = cFieldDefaultBC
  lblItemDesc.Label.ForeColor = cFieldDefaultFC
  '
  lblMaxQty.Caption = ""
  lblMaxQty.Label.BackColor1 = cFieldDefaultBC
  lblMaxQty.Label.ForeColor = cFieldDefaultFC
  '
  txtAccept.Visible = False
  '
  msZJDEMAIN = App.GetValue("Vers")
  msPgm = App.GetValue("Pgm")
  '
  mnStatusT = CCur(GetProcOpt(msPgm,msZJDEMAIN,"3;1"))

  If gbPOError Then
    App.ExitForm
    Exit Sub
  End If

  msDOCTYPE = GetProcOpt(msPgm,msZJDEMAIN,"2;1",sHeader)

  If gbPOError Then
    App.ExitForm
    Exit Sub
  End If
  '
  If  App.ClientType = "GUI" Then
    Form.Caption = Left(sHeader,29)
  Else
    Form.Caption = Left(sHeader,20)
  End If

End Sub

Private Sub Form_LostFocus()
  On Error Resume Next
  '
  Dim iCN As Integer
  '
  iCN = App.PromptNo
  RFPrompt(iCN).BorderStyle = DisplayTransparent
  RFPrompt(iCN).BackColor1 = cFieldDefaultBC
  RFPrompt(iCN).ForeColor = cFieldDefaultFC

End Sub

Private Sub Form_OnUpdate(ByRef Cancel As Boolean)
  On Error Resume Next
  Dim sSQL     As String
  Dim sCols    As String
  Dim sRows    As String
  Dim sUser    As String
  Dim nErrNo   As Long
  Dim sOPSQ    As String
  '
  Dim emProc As New EmbeddedProc
  '
  ' Write workfile if new, else delete and run macro
  If bNewWO Then
    sUser = App.User
    sOPSQ = Replace(App.GetValue("tmOPSQ"),",", "")
    sOPSQ = Replace(sOPSQ,".","")
    sOPSQ = CStr(ConvDecimalsFromSQL("OPSQ", sOPSQ))
    sOPSQ = Replace(sOPSQ,",",".")
    sSQL = "insert into RF31122 Values('%tmMCU', %tmDOCO, '%tmDCTO', " & sOPSQ & ", '%tmSHIFT', '%tmHRTYPE', %tmEMPL, '%tmSTime', '" & sUser & "' )"
  Else
    emProc.Name                  = "TWOTE0100"
    emProc.DataSource            = gsDataSource
    emProc.Param("tmCO")         = App.GetValue("tmCO")
    emProc.Param("tmMCU")        = App.GetValue("tmMCU")
    emProc.Param("tmDOCO")       = App.GetValue("tmDOCO")
    emProc.Param("tmDCTO")       = App.GetValue("tmDCTO")
    emProc.Param("tmOPSQ")       = App.GetValue("tmOPSQ")
    emProc.Param("tmEMPL")       = App.GetValue("tmEMPL")
    emProc.Param("tmSTART")      = App.GetValue("tmSTIME")
    emProc.Param("tmEND")        = App.GetValue("tmETIME")
    emProc.Param("tmDOCTYPE")    = msDOCTYPE
    emProc.Param("tmSHFT")       = App.GetValue("tmSHIFT")
    emProc.Param("tmHRTP")       = App.GetValue("tmHRTYPE")
    emProc.Param("tmWODT")       = App.GetValue("tmWODT")
    emProc.Param("tmUSER")       = App.User
    emProc.Param("tmQTY")        = App.GetValue("tmQty")
    emProc.Param("tmSCRAP")      = App.GetValue("tmScrap")
    emProc.Param("tmZVERSION")   = msZJDEMAIN
    emProc.Param("tmPGM")        = msPgm
    If Ext(gsLog,1) = "1" Then Call TranLog(cTNId, "1", emProc.Name, "", emProc)
    If gbQueueProcessing Then
      If gbQLoadBal Then emProc.QueueName = RFQBalance("", 0)
      
      If Not emProc.Queue Then
        App.MsgBox GetMsg(222) & SysErr.Description
        Cancel = True
        App.SetFocus "txtAccept"
        Exit Sub
      End If
    Else
      If Not emProc.Execute Then
        App.MsgBox GetMsg(221) & Trim(emProc.Param("tmERRTEXT"))
        If Ext(gsLog,2) = "1" Then Call TranLog(cTNId, "2", emProc.Name, "", emProc)
        Cancel = True
        App.SetFocus "txtAccept"
        Exit Sub
      End If
    End If
    sSQL = "delete from RF31122 where WTMMCU = '%tmMCU' and WTDOCO = %tmDOCO and WTDCTO = '%tmDCTO' and WTAN8 = %tmEMPL "
  End If

  nErrNo = DB.Execute(sSQL)

  If nErrNo <> 0 Then
    App.MsgBox GetMsg(242)
  End If

  bNewWO = False

  lblItem.Caption     = ""
  lblItemDesc.Caption = ""

End Sub

Private Sub txtMCU_GotFocus(ByRef Rsp As String, ByRef AllowChange As Boolean)
  On Error Resume Next
  '
  If (Len(Rsp)=0) Then Rsp = GetDefaultMCU()
  If Len(Rsp) > 0 Then AllowChange = False
End Sub

Private Sub txtMCU_OnBackup(ByRef Cancel As Boolean)
  On Error Resume Next
  '
  App.SetValue("tmMCU",  "")
  App.SetValue("tmCO",   "")
  App.SetValue("tmCSMT", "")

  Call Clear_DependentFields

End Sub

Private Sub txtMCU_OnEnter(ByRef Rsp As String, ByRef Cancel As Boolean, ByRef ErrMsg As String)
  On Error Resume Next
  '
  Dim sCompany As String
  Dim sMCU     As String
  Dim sSQL     As String
  Dim sCols    As String
  Dim sRows    As String
  '
  If (Len(Rsp)=0) Then Exit Sub
  '
  Cancel = True
  If Not Validate_BranchPlant(Rsp, sMCU, sCompany) Then Exit Sub
  '
  ' Get cost method for this branch plant
  sSQL = "select CICSMT from  F41001" & _
         " where CIMCU = '" &  sMCU & "'"

  DB.Execute(sSQL,sCols,sRows)
  '
  If (Len(sRows)=0) Then
    App.MsgBox GetMsg(10)
    Exit Sub
  End If
  '
  App.SetValue("tmCO",   sCompany)
  App.SetValue("tmMCU",  sMCU)
  App.SetValue("tmCSMT", DB.Extract(sCols,sRows,1,"CICSMT"))
  '
  Cancel = False
End Sub

Private Sub txtMCU_OnSearch(ByRef Rsp As String, ByRef Cancel As Boolean)
  On Error Resume Next
  '
  Cancel = Not Search_BranchPlant(Rsp)
End Sub

Private Sub txtEMPL_OnBackup(ByRef Cancel As Boolean)
  On Error Resume Next
  '
  App.SetValue("tmEMPL",  "")
  App.SetValue("tmSHIFT", "")

End Sub

Private Sub txtEMPL_OnEnter(ByRef Rsp As String, ByRef Cancel As Boolean, ByRef ErrMsg As String)
  On Error Resume Next
  '
  Dim sShift As String
  Dim sSQL   As String
  Dim sCols  As String
  Dim sRows  As String
  '
  If (Len(Rsp)=0) Then Exit Sub

  Cancel = True
  '
  sSQL = "select count(*) from  F0101" & _
         " where ABAN8 = " & Val(Rsp)

  DB.Execute(sSQL, sCols, sRows)
  '
  If (Val(sRows) = 0) Then
    App.MsgBox GetMsg(59)
    Exit Sub
  End If
  '
  sSQL = "select ABAN8,ABAC29 from F0101" & _
         " where ABAN8 = " & Rsp & " and (ABAT1 = 'L' or ABAT1 = 'E')"

  DB.Execute(sSQL,sCols,sRows)
  '
  If (Val(sRows) = 0) Then
    App.MsgBox GetMsg(59)
    Exit Sub
  End If
  '
  sShift = DB.Extract(sCols,sRows,1,"ABAC29")
  '
  App.SetValue("tmEMPL",  Rsp)
  App.SetValue("tmSHIFT", sShift)

  Cancel = False

End Sub

Private Sub txtEMPL_OnSearch(ByRef Rsp As String, ByRef Cancel As Boolean)
  On Error Resume Next
  '
  Dim sMCU As String
  '
  sMCU = App.GetValue("tmMCU")
  Cancel = Not Search_F0101_Employee(Rsp, sMCU)
End Sub

Private Sub txtDOCO_OnBackup(ByRef Cancel As Boolean)
  On Error Resume Next
  '
  App.SetValue("tmDOCO", "")

  Call Clear_DependentFields

End Sub

Private Sub txtDOCO_OnEnter(ByRef Rsp As String, ByRef Cancel As Boolean, ByRef ErrMsg As String)
  On Error Resume Next
  '
  Dim sSQL  As String
  Dim sCols As String
  Dim sRows As String
  '
  If (Len(Rsp)=0) Then Exit Sub

  Cancel = True

  Rsp = UCase(Rsp)
  '
  sSQL = "select WADCTO from  F4801" & _
         " where WADOCO = " & Rsp & " and WAMCU='%tmMCU'"

  DB.Execute(sSQL, sCols, sRows)
  '
  If (Len(sRows) = 0) Then
    App.MsgBox(GetMsg(228))
    Exit Sub
  End If
  '
  ' If Order Number is unique then default DocType
  If (DB.Count(sRows) = 1) Then
    txtDCTO.Text = DB.Extract(sCols, sRows, 1, "WADCTO")
  End If
  '
  App.SetValue("tmDOCO", Rsp)
  'If Not Validate_WorkOrder Then Exit Sub
  Cancel = False
End Sub

Private Sub txtDOCO_OnSearch(ByRef Rsp As String, ByRef Cancel As Boolean)
  On Error Resume Next
  '
  Dim sDocType  As String
  Dim sMCU      As String
  '
  sMCU = App.GetValue("tmMCU")
  '
  Cancel = Not Search_F4801_DOCO(Rsp, sMCU, cStatusF, mnStatusT, sDocType)
  If Not Cancel Then txtDCTO.Text = sDocType
End Sub

Private Sub txtDCTO_OnBackup(ByRef Cancel As Boolean)
  On Error Resume Next
  '
  App.SetValue("tmDCTO",  "")

  Call Clear_DependentFields

End Sub

Private Sub txtDCTO_OnEnter(ByRef Rsp As String, ByRef Cancel As Boolean, ByRef ErrMsg As String)
  On Error Resume Next

  Dim sSQL  As String
  Dim sCols As String
  Dim sRows As String
  Dim sTime As String
  Dim sDate As String
  Dim nDOCO As Long
  Dim sDCTO As String
  Dim vRsp  As Variant
  '
  Cancel = True
  '
  If (Len(Rsp)=0) Then Exit Sub
  '
  App.SetValue("tmDCTO", Rsp)
  If Not Validate_WorkOrder Then Exit Sub
  '
  ' check if WO already exists in workfile
  sSQL = "select WTDOCO, WTDCTO from RF31122 where WTAN8 = %tmEMPL and WTMMCU = '%tmMCU' "

  DB.Execute(sSQL,sCols,sRows)

  If Len(sRows) <> 0 Then
    nDOCO = DB.Extract(sCols,sRows,1,1)
    sDCTO = Trim(DB.Extract(sCols,sRows,1,2))
    If nDOCO <> App.GetValue("tmDOCO") Or sDCTO <> App.GetValue("tmDCTO") Then
      vRsp = App.MsgBox(GetMsg(243) & CStr(DB.Extract(sCols,sRows,1,1)) & "/" & DB.Extract(sCols,sRows,1,2) & vbCrLf & "Continue?",vbYesNo,1)
      If vRsp = vbNo Then
        App.SetFocus("txtDOCO")
        Exit Sub
      End If
    End If
  End If
  '
  sSQL = "select WTOPSQ, WTSHFT, WTTTYR, WTUPMJ from RF31122 where WTDOCO = %tmDOCO and WTDCTO = '%tmDCTO' and WTAN8 = %tmEMPL and WTMMCU = '%tmMCU' "

  DB.Execute(sSQL,sCols,sRows)

  If Len(sRows) = 0 Then      'record does not exist -> create new start time entry
    txtSTIME.DisplayOnly  = False
    txtETIME.Visible      = False
    txtETIME.Required     = False
    txtQty.Visible        = False
    txtQty.Required       = False
    lblMaxQty.Visible      = False
    bNewWO = True
  Else                        'record exists -> populate fields
    txtOPSQ.Text   = DB.Extract(sCols,sRows,1,1)
    txtSHIFT.Text  = DB.Extract(sCols,sRows,1,2)
    txtHRTYPE.Text = DB.Extract(sCols,sRows,1,3)
    txtSTIME.Text  = DB.Extract(sCols,sRows,1,4)
    txtSTIME.DisplayOnly  = True
    txtETIME.Visible      = True
    txtETIME.Required     = True
    txtQty.Visible        = True
    txtQty.Required       = True
    lblMaxQty.Visible      = True
    bNewWO = False
  End If
  '
  Cancel = False

End Sub

Private Sub txtDCTO_OnSearch(ByRef Rsp As String, ByRef Cancel As Boolean)
  On Error Resume Next
  '
  Dim sMCU  As String
  Dim vDoco As Variant
  '
  sMCU = App.GetValue("tmMCU")
  Cancel = Not Search_F4801_DCTO(Rsp, sMCU, cStatusF, mnStatusT, vDoco)
End Sub

Private Sub txtOPSQ_OnBackup(ByRef Cancel As Boolean)
  On Error Resume Next
  '
  App.SetValue("tmOPSQ", "")

End Sub

Private Sub txtOPSQ_OnEnter(ByRef Rsp As String, ByRef Cancel As Boolean, ByRef ErrMsg As String)
  On Error Resume Next
  '
  Dim nOper As Currency
  Dim sMCU  As String
  Dim sSQL  As String
  Dim sCols As String
  Dim sRows As String
  '
  If (Len(Rsp)=0) Then Exit Sub

  Cancel = True
  '
  nOper = ConvDecimalsToSQL("OPSQ", Rsp)
  Rsp = FormatDecimals("OPSQ", Rsp)
  '
  ' Gather routing information for all operations
  sSQL = "select WLMCU, WLOPSQ from  F3112" & _
         " where WLDOCO = %tmDOCO and WLDCTO = '%tmDCTO' and WLOPSQ = " & nOper & _
         " order by WLOPSQ"

  DB.Execute(sSQL,sCols,sRows)
  '
  If (Len(sRows) = 0) Then
    App.MsgBox GetMsg(81)
    Exit Sub
  End If
  '
  ' Check to see if work center rates are set up for this branch plant/cost method/work center
  sMCU = DB.Extract(sCols,sRows,1,"WLMCU")
  '
  sSQL = "select count(*) from  F30008" & _
         " where IWMCU = '" & sMCU & "' and IWLEDG = '%tmCSMT'"

  DB.Execute(sSQL,sCols,sRows)
  '
  If (Val(sRows) = 0) Then
    App.MsgBox GetMsg(241)
    Exit Sub
  End If
  '
  ' get qty from current record
  mnMaxQty = 0

  sSQL = "select sum(WTSOQS), WTUOM from F31122 where WTDOCO = %tmDOCO and WTDCTO = '%tmDCTO' and WTTYR = '4' and WTOPSQ = " & nOper & _
         " Group by WTOPSQ, WTUOM Order by WTOPSQ DESC"

  DB.Execute(sSQL,sCols,sRows)

  If Len(sRows) > 0 Then
    mnMaxQty = ConvDecimalsFromSQL("SOQS", DB.Extract(sCols,sRows,1,1) * -1)
    lblMaxQty.Caption = CStr(mnMaxQty) & " " & DB.Extract(sCols,sRows,1,2)
  End If
  '
  ' get qty from privious record
  sSQL = "select sum(WTSOQS), WTUOM from F31122 where WTDOCO = %tmDOCO and WTDCTO = '%tmDCTO' and WTTYR = '4' and WTOPSQ < " & nOper & _
         " Group by WTOPSQ, WTUOM Order by WTOPSQ DESC"

  DB.Execute(sSQL,sCols,sRows)

  If Len(sRows) > 0 Then
    mnMaxQty = mnMaxQty + ConvDecimalsFromSQL("SOQS", DB.Extract(sCols,sRows,1,1))
    lblMaxQty.Caption = CStr(mnMaxQty) & " " & DB.Extract(sCols,sRows,1,2)
  Else
    mnMaxQty = 999999
    lblMaxQty.Caption = ""
  End If
  '
  App.SetValue("tmOPSQ", Rsp)
  '
  Cancel = False

End Sub

Private Sub txtOPSQ_OnSearch(ByRef Rsp As String, ByRef Cancel As Boolean)
  On Error Resume Next
  '
  Dim sMCU  As String
  Dim sDOCO As String
  Dim sDCTO As String
  '
  sMCU  = App.GetValue("tmMCU")
  sDOCO = App.GetValue("tmDOCO")
  sDCTO = App.GetValue("tmDCTO")
  '
  Cancel = Not Search_F3112(Rsp, sMCU, sDOCO, sDCTO)
End Sub

Private Sub txtQty_OnBackup(ByRef Cancel As Boolean)
  On Error Resume Next
  '
  App.SetValue("tmQTY",0)

End Sub

Private Sub txtQty_OnEnter(ByRef Rsp As String, ByRef Cancel As Boolean, ByRef ErrMsg As String)
  On Error Resume Next
  '
  Dim nQty As Currency
  '
  If (Len(Rsp)=0) Then Exit Sub

  Cancel=True
  '
  ' Numeric?
  If Not IsNumeric(Rsp) Then
    App.MsgBox GetMsg(181)
    Exit Sub
  End If
  '
  If Val(Rsp) > mnMaxQty Then
    App.MsgBox GetMsg(244)
    Exit Sub
  End If
  '
  nQty = Val(Rsp)
  '
  App.SetValue("tmQTY",Val(Rsp))
  '
  Cancel = False

End Sub

Private Sub txtSHIFT_OnBackup(ByRef Cancel As Boolean)
  On Error Resume Next
  '
  App.SetValue("tmSHIFT", "")

End Sub

Private Sub txtSHIFT_OnEnter(ByRef Rsp As String, ByRef Cancel As Boolean, ByRef ErrMsg As String)
  On Error Resume Next
  '
  Dim sDRDL01 As String
  Dim sDRDL02 As String
  '
  If (Len(Rsp)=0) Then Exit Sub
  Cancel=True
  '
  Rsp = FixRight(FixRight(Trim(UCase(Rsp)), 3, " "), 10, " ")

  If gsJDEVers = "A9.2" Then
    If Not Validate_UDC(Rsp, "07", "SH", sDRDL01, sDRDL02) Then Exit Sub
  Else
    If Not Validate_UDC(Rsp, "06", "SH", sDRDL01, sDRDL02) Then Exit Sub
  End If
  '
  App.SetValue("tmSHIFT", Rsp)
  '
  Cancel = False
  '
End Sub

Private Sub txtSHIFT_OnSearch(ByRef Rsp As String, ByRef Cancel As Boolean)
  On Error Resume Next
  '
  If gsJDEVers = "A9.2" Then
    Cancel = Not Search_UDC(Rsp, "07", "SH")
  Else
    Cancel = Not Search_UDC(Rsp, "06", "SH")
  End If

End Sub

Private Sub txtHRTYPE_OnBackup(ByRef Cancel As Boolean)
  On Error Resume Next
  '
  App.SetValue("tmHRTYPE", "")

End Sub

Private Sub txtHRTYPE_OnEnter(ByRef Rsp As String, ByRef Cancel As Boolean, ByRef ErrMsg As String)
  On Error Resume Next
  '
  Dim sDRDL01 As String
  Dim sDRDL02 As String
  '
  If (Len(Rsp)=0) Then Exit Sub

  Cancel = True
  '
  Rsp = FixRight(FixRight(Trim(UCase(Rsp)), 3, " "), 10, " ")

  If Not Validate_UDC(Rsp, "31", "TY", sDRDL01, sDRDL02) Then Exit Sub
  '
  App.SetValue("tmHRTYPE", Rsp)
  '
  Cancel = False
  '
End Sub

Private Sub txtHRTYPE_OnSearch(ByRef Rsp As String, ByRef Cancel As Boolean)
  On Error Resume Next
  '
  Cancel = Not Search_UDC(Rsp, "31", "TY")
End Sub

Private Sub txtSTIME_OnBackup(ByRef Cancel As Boolean)
  On Error Resume Next
  '
  App.SetValue("tmSTIME", "")

End Sub

Private Sub txtSTIME_OnEnter(ByRef Rsp As String, ByRef Cancel As Boolean, ByRef ErrMsg As String)
  On Error Resume Next
  '
  If (Len(Rsp)=0) Then
    Rsp = Format(Time,"HH:NN")
    Exit Sub
  End If

  Cancel = True
  '
  Rsp = Replace(Rsp, ".", "")
  Rsp = Replace(Rsp, "-", "")
  Rsp = Replace(Rsp, ":", "")
  Rsp = Replace(Rsp, "+", "")
  '
  If IsNumeric(Rsp) = False Then
    App.MsgBox GetMsg(85)
    Exit Sub
  End If
  '
  If Val(Rsp) < 0 Or Val(Rsp) > 240000 Then
    App.MsgBox GetMsg(85)
    Exit Sub
  End If
  '
  Rsp = FixLeft(Rsp,4,"0")
  App.SetValue("tmSTIME", Rsp)
  Rsp = Format(Rsp,"##:##")
  '
  If cUse24Hrs Then
    Rsp = Format(CStr(Rsp), "hh:mm")
  Else
    Rsp = Format(CStr(Rsp), "hh:mm AM/PM")
  End If
  '
  Cancel = False

End Sub

Private Sub txtSTIME_OnSearch(ByRef Rsp As String, ByRef Cancel As Boolean)
  On Error Resume Next
  '
  Rsp = Format(Time,"hhmm")

End Sub

Private Sub txtETIME_OnBackup(ByRef Cancel As Boolean)
  On Error Resume Next
  '
  App.SetValue("tmETIME", "")

End Sub

Private Sub txtETIME_OnEnter(ByRef Rsp As String, ByRef Cancel As Boolean, ByRef ErrMsg As String)
  On Error Resume Next
  On Error Resume Next
  '
  If (Len(Rsp)=0) Then
    Rsp = Format(Time,"HH:NN")
    Exit Sub
  End If

  Cancel = True
  '
  Rsp = Replace(Rsp, ".", "")
  Rsp = Replace(Rsp, "-", "")
  Rsp = Replace(Rsp, ":", "")
  Rsp = Replace(Rsp, "+", "")
  '
  If IsNumeric(Rsp) = False Then
    App.MsgBox GetMsg(85)
    Exit Sub
  End If
  '
  If Val(Rsp) < 0 Or Val(Rsp) > 2400 Then
    App.MsgBox GetMsg(85)
    Exit Sub
  End If
  '
  Rsp = FixRight(Rsp,4,"0")
  App.SetValue("tmETIME", Rsp)
  Rsp = Format(Rsp,"##:##")
  '
  If cUse24Hrs Then
    Rsp = Format(CStr(Rsp), "hh:mm")
  Else
    Rsp = Format(CStr(Rsp), "hh:mm AM/PM")
  End If
  '
  Cancel = False

End Sub

Private Sub txtETIME_OnSearch(ByRef Rsp As String, ByRef Cancel As Boolean)
  On Error Resume Next
  '
  Rsp = Format(Time,"hhmm")

End Sub

Private Sub txtAccept_GotFocus(ByRef Rsp As String, ByRef AllowChange As Boolean)
  On Error Resume Next
  '
  txtAccept.Visible = True

End Sub

Private Sub txtAccept_LostFocus()
  On Error Resume Next
  '
  txtAccept.Visible = False

End Sub

Private Sub txtAccept_OnEnter(ByRef Rsp As String, ByRef Cancel As Boolean, ByRef ErrMsg As String)
  On Error Resume Next
  '
  Dim iRow As Integer
  '
  iRow = txtAccept.Label.Top
  '
  Screen.Print 0, iRow, "Processing...", False, True, True

End Sub

Private Sub Clear_DependentFields
  On Error Resume Next
  '
  lblItem.Caption     = ""
  lblItemDesc.Caption = ""

End Sub

Private Function Validate_WorkOrder As Boolean
  On Error Resume Next
  '
  Dim nWoStatus As Long
  Dim sMCU      As String
  Dim sDOCO     As String
  Dim sDCTO     As String
  Dim sSQL      As String
  Dim sCols     As String
  Dim sRows     As String
  '
  sMCU  = App.GetValue("tmMCU")
  sDOCO = App.GetValue("tmDOCO")
  sDCTO = App.GetValue("tmDCTO")
  '
  If (Len(sMCU)=0 Or Len(sDOCO)=0 Or Len(sDCTO)=0) Then
    Call Clear_DependentFields
    Validate_WorkOrder = True
    Exit Function
  End If
  '
  Validate_WorkOrder = False
  '
  sSQL="select WASRST,WALITM,WADL01 from  F4801" & _
       " where WADOCO = " & sDOCO & " and WADCTO = '" & sDCTO & "' and WAMCU='" & sMCU & "'"

  DB.Execute(sSQL,sCols,sRows)
  '
  If (Len(sRows) = 0) Then
    App.MsgBox GetMsg(226)
    Exit Function
  End If
  '
  ' Check Work Order Status
  nWoStatus = Val(Trim(DB.Extract(sCols, sRows, 1, "WASRST")))
  '
  If (nWoStatus > mnStatusT) Then
    App.MsgBox GetMsg(230)
    Exit Function
  End If
  '
  If (nWoStatus < cStatusF) Then
    App.MsgBox GetMsg(231)
    Exit Function
  End If
  '
  lblItem.Caption     = DB.Extract(sCols, sRows, 1, "WALITM")
  lblItemDesc.Caption = DB.Extract(sCols, sRows, 1, "WADL01")
  '
  Validate_WorkOrder = True

End Function

Private Sub txtWODT_OnEnter(ByRef Rsp As String, ByRef Cancel As Boolean, ByRef ErrMsg As String)
  On Error Resume Next
  '
  Dim sExpDt As String
  '
  If (Len(Rsp)=0) Then Exit Sub

  Cancel = True
  '
  Rsp = Trim(Rsp)
  sExpDt = CStr(CDate(Rsp))
  '
  If (Len(sExpDt)=0) Then
    App.MsgBox GetMsg(57)
    Exit Sub
  End If
  '
  Rsp = sExpDt

  App.SetValue("tmWODT", Rsp)
  '
  Cancel = False

End Sub
</Code>
